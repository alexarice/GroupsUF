\documentclass{article}

\usepackage{agda}
% \usepackage{fancyvrb}
\usepackage{amsthm}
\usepackage{amsmath}
% \DefineVerbatimEnvironment{code}{Verbatim}{}

\usepackage[utf8]{inputenc}

\usepackage{newunicodechar}
\newunicodechar{λ}{\ensuremath{\mathnormal\lambda}}
\newunicodechar{ℓ}{\ensuremath{\mathnormal\ell}}
\newunicodechar{𝓖}{\ensuremath{\mathcal{G}}}
\newunicodechar{≡}{\ensuremath{\equiv}}
\newunicodechar{⟨}{\ensuremath{\langle}}
\newunicodechar{⟩}{\ensuremath{\rangle}}
\newunicodechar{⌊}{\ensuremath{\lfloor}}
\newunicodechar{⌋}{\ensuremath{\rfloor}}
\newunicodechar{→}{\ensuremath{\mathnormal\to}}
\newunicodechar{∀}{\ensuremath{\mathnormal\forall}}
\newunicodechar{∎}{\ensuremath{\mathnormal\qed}}
\newunicodechar{∘}{\ensuremath{\mathnormal\circ}}
\newunicodechar{⁻}{\ensuremath{\mathnormal{}^{-}}}
\newunicodechar{¹}{\ensuremath{\mathnormal{}^{1}}}
\newunicodechar{≈}{\ensuremath{\mathnormal\approx}}
\newunicodechar{′}{\ensuremath{\mathnormal\prime}}

\title{Strictly Associative Group Theory using Univalence}
\author{Alex Rice}
\begin{document}
\maketitle

Often when proving basic properties about groups, we end up proving equations using equational reasoning. Unfortunately when formalising this in a proof assistant, most steps of the proof are for managing brackets and identities around our expression, which is annoying to work with and obscures the actual meaningful steps of the proof.

The usual approaches to these problems involve either putting up with the problem, using a variety of tactics to make the manipulations less painful, or having an array of helper lemmas. None of these methods mirror the approach used in traditional mathematics where it would be very rare to see a bracket written out in a proof.

As an example consider this proof that proves that for \(x,y\) in some group \(G\) that \((x \cdot y)^{-1} = y^{-1} \cdot x^{-1}\):

\begin{code}[hide]
module Abstract.Abstract where
open import Cubical.Algebra.Group.Base
open import Cubical.Algebra.Group.Properties
open import Cubical.Foundations.Structure
open import Cubical.Foundations.Prelude
open import Groups.Symmetric.Representable
\end{code}

\begin{code}
module _ {ℓ} {𝓖 : Group ℓ} where
  open GroupStr (snd 𝓖)
  open GroupTheory 𝓖

  lemma : (x y : ⟨ 𝓖 ⟩) → (inv (x · y)) ≡ (inv y · inv x)
  lemma x y = ·CancelL (x · y) ((x · y) · inv (x · y)
     ≡⟨ invr (x · y) ⟩
    1g
     ≡⟨ sym (invr x) ⟩
    (x · inv x)
     ≡⟨ cong (_· inv x) (sym (rid x)) ⟩
    ((x · 1g) · inv x)
     ≡⟨ cong (λ - → (x · -) · inv x) (sym (invr y)) ⟩
    ((x · (y · inv y)) · inv x)
     ≡⟨ cong (_· inv x) (assoc x y (inv y)) ⟩
    (((x · y) · inv y) · inv x)
     ≡⟨ sym (assoc (x · y) (inv y) (inv x)) ⟩
    ((x · y) · inv y · inv x) ∎)
\end{code}

Here we use a lemma to cancel off a \(x \cdot y\) term off the front (which itself involves an associativity move) as well as two associativity moves in the actual proof.

In this work, we present an alternative solution. For every group \(\mathcal{G}\) we are able to find an isomorphic group for which associativity and unitality hold judgmentally. This leverages Cayley's Theorem, which states that every group is isomorphic to a subgroup of a permutation group. By representing permutations as functions, we are able to ensure that associativity and unitality hold ``on the nose''. I will discuss how this can be done and some of the challenges that one faces.

This then allows us to use univalence to obtain that any group is not only isomorphic but actually equal to a group with strict associativity and unitality, which allows us to prove theorems about groups by only proving the theorems for the strictified group. As an example the code above can be reduced to the following:
\begin{code}
InvOfComp : ∀ {ℓ} → (𝓖 : Group ℓ) → Type ℓ
InvOfComp 𝓖 = ∀ g h → inv (g · h) ≡ inv h · inv g
  where open GroupStr (snd 𝓖)

inv-of-comp : ∀ {ℓ} → (𝓖 : Group ℓ) → InvOfComp 𝓖
inv-of-comp 𝓖 = strictify InvOfComp
  λ g h → begin
    (g ∘ h) ⁻¹ ∘⌊⌋                      ≈⌊ sym (rinv g) ⌋
    (g ∘ h) ⁻¹ ∘ g ∘⌊⌋∘ g ⁻¹            ≈⌊ sym (rinv h) ⌋
    ⌊ (g ∘ h) ⁻¹ ∘ g ∘ h ⌋∘ h ⁻¹ ∘ g ⁻¹ ≈⌊ linv (g ∘ h) ⌋
    h ⁻¹ ∘ g ⁻¹                      ∎′
      where open import Groups.Reasoning 𝓖
\end{code}



\end{document}
