We show that there is an inclusion from any group into into the Symmetric group of its underlying set.

<details>
<summary>Module header</summary>

```agda
{-# OPTIONS --safe --cubical #-}

open import Cubical.Algebra.Group

module Groups.Symmetric.Inclusion {â„“} (ğ“– : Group â„“) where

open import Cubical.Data.Sigma
open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Structure
open import Cubical.Functions.FunExtEquiv
open import Groups.Function.Inverse
open import Groups.Symmetric

open GroupStr (ğ“– .snd)
```

</details>

```agda
SymGroup : Group â„“
SymGroup = Symmetric-Group âŸ¨ ğ“– âŸ© (isSetGroup ğ“–)
```

The inclusion takes `g` to the function `Î» x â†’ g Â· x` with inverse `Î» x â†’ g â»Â¹ Â· x`

```agda
inc : âŸ¨ ğ“– âŸ© â†’ âŸ¨ SymGroup âŸ©
inc g = (Î» x â†’ g Â· x) , (Î» x â†’ inv g Â· x) , i , ii
  where
    i : (b x : âŸ¨ ğ“– âŸ©) â†’ x â‰¡ inv g Â· b â†’ g Â· x â‰¡ b
    i b x p =
      g Â· x          â‰¡âŸ¨ cong (g Â·_) p âŸ©
      g Â· (inv g Â· b) â‰¡âŸ¨ assoc g (inv g) b âŸ©
      (g Â· inv g) Â· b â‰¡âŸ¨ cong (_Â· b) (invr g) âŸ©
      1g Â· b          â‰¡âŸ¨ lid b âŸ©
      b âˆ

    ii : (a y : âŸ¨ ğ“– âŸ©) â†’ y â‰¡ g Â· a â†’ inv g Â· y â‰¡ a
    ii a y p =
      inv g Â· y       â‰¡âŸ¨ cong (inv g Â·_) p âŸ©
      inv g Â· (g Â· a) â‰¡âŸ¨ assoc (inv g) g a âŸ©
      (inv g Â· g) Â· a â‰¡âŸ¨ cong (_Â· a) (invl g) âŸ©
      1g Â· a          â‰¡âŸ¨ lid a âŸ©
      a âˆ
```

## Inclusion properties

The inclusion can be shown to be injective and a group homomorphism.

```agda
inc-injective : (x y : âŸ¨ ğ“– âŸ©) â†’ inc x â‰¡ inc y â†’ x â‰¡ y
inc-injective x y p =
  x     â‰¡âŸ¨ sym (rid x) âŸ©
  x Â· 1g â‰¡âŸ¨ cong (Î» a â†’ fst a 1g) p âŸ©
  y Â· 1g â‰¡âŸ¨ rid y âŸ©
  y âˆ

open GroupStr (SymGroup .snd) using () renaming (_Â·_ to _Â·â€²_; 1g to 1gs; inv to invs)
inc-homo : (x y : âŸ¨ ğ“– âŸ©) â†’ inc (x Â· y) â‰¡ (inc x) Â·â€² (inc y)
inc-homo x y = inverse-equality-lemma _ _ (isSetGroup ğ“–) (isSetGroup ğ“–)
  Î» g â†’ sym (assoc x y g)

inc-pres1 : inc 1g â‰¡ 1gs
inc-pres1 = inverse-equality-lemma (inc 1g) 1gs (isSetGroup ğ“–) (isSetGroup ğ“–) lid

inc-pres-inv : (g : âŸ¨ ğ“– âŸ©) â†’ inc (inv g) â‰¡ invs (inc g)
inc-pres-inv g = inverse-equality-lemma (inc (inv g)) (invs (inc g)) (isSetGroup ğ“–) (isSetGroup ğ“–) (Î» x â†’ refl)
```
