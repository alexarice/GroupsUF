{-# OPTIONS --safe --cubical #-}

open import Cubical.Structures.Group

module Groups.Symmetric.Representable {â„“} (ğ“– : Group {â„“}) where

open import Cubical.Data.Sigma
open import Cubical.Data.Vec
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Prelude
open import Cubical.Foundations.SIP
open import Cubical.Functions.FunExtEquiv
open import Function.Inverse
open import Groups.Symmetric
open import Groups.Symmetric.Inclusion ğ“–

open group-Â·syntax ğ“–
open group-operation-syntax

Representable : âŸ¨ SymGroup âŸ© â†’ Type â„“
Representable f = âˆ€ x g h â†’ x â‰¡ g Â· h â†’ fst f x â‰¡ fst f g Â· h

Repr : Type â„“
Repr = Î£[ f âˆˆ âŸ¨ SymGroup âŸ© ] Representable f

-- Representable is a Prop and so Repr is a set

rep-prop : (f : âŸ¨ SymGroup âŸ©) â†’ isProp (Representable f)
rep-prop f = isPropÎ 2 (Î» x y â†’ isPropÎ 2 Î» w z â†’ (group-is-set ğ“– (fst f x) (fst f y Â· w)))

repÎ£-set : isSet Repr
repÎ£-set = isSetÎ£ (group-is-set SymGroup) Î» f â†’ isPropâ†’isSet (rep-prop f)

-- Representable elments are closed under group operations

rep-comp : âˆ€ (f fâ€² : Repr) â†’ Repr
rep-comp (f , rf) (fâ€² , rfâ€²) = f Â·âŸ¨ SymGroup âŸ© fâ€² , Î» x g h p â†’ rf (fst fâ€² x) (fst fâ€² g) h (rfâ€² x g h p)

rep-id : Repr
rep-id = group-id SymGroup , Î» x g h p â†’ p

rep-inv : (f : Repr) â†’ Repr
rep-inv (a@(f , finv , Îµ , Î·) , rf) = (group-inv SymGroup a) ,
  Î» x g h p â†’ Î· (finv g Â· h) x
   (x              â‰¡âŸ¨ p âŸ©
    g Â· h          â‰¡âŸ¨ cong (_Â· h) (sym (Îµ g (finv g) refl)) âŸ©
    f (finv g) Â· h â‰¡âŸ¨ sym (rf (finv g Â· h) (finv g) h refl) âŸ©
    f (finv g Â· h) âˆ)

-- Associativity and Unitality still hold by definition

rep-assoc : (f g h : Repr) â†’ rep-comp f (rep-comp g h) â‰¡ rep-comp (rep-comp f g) h
rep-assoc f g h = refl

rep-lid : (f : Repr) â†’ rep-comp rep-id f â‰¡ f
rep-lid f = refl

rep-rid : (f : Repr) â†’ rep-comp f rep-id â‰¡ f
rep-rid f = refl

-- Reprs are equal if the underlying object is

repr-equality : (f g : Repr) â†’ fst f â‰¡ fst g â†’ f â‰¡ g
repr-equality (f , fr) (g , gr) p = Î£PathP (p , (isPropâ†’PathP (Î» i â†’ rep-prop (p i)) fr gr))

-- And so the inverses work as expected

rep-inv-left : (f : Repr) â†’ rep-comp (rep-inv f) f â‰¡ rep-id
rep-inv-left f = repr-equality (rep-comp (rep-inv f) f) rep-id (group-linv SymGroup (fst f))

rep-inv-right : (f : Repr) â†’ rep-comp f (rep-inv f) â‰¡ rep-id
rep-inv-right f = repr-equality (rep-comp f (rep-inv f)) rep-id (group-rinv SymGroup (fst f))

-- and hence form a group

RSymGroup : Group {â„“}
RSymGroup =
  Repr ,
  rep-comp ,
  (repÎ£-set , rep-assoc) ,
  rep-id ,
  (Î» g â†’ rep-lid g , rep-rid g) ,
  (Î» x â†’ (rep-inv x , rep-inv-right x , rep-inv-left x))

-- Any included element is Representable

inc-rep : âˆ€ (a : âŸ¨ ğ“– âŸ©) â†’ Representable (inc a)
inc-rep a x g h p =
  a Â· x â‰¡âŸ¨ cong (a Â·_) p âŸ©
  a Â· (g Â· h) â‰¡âŸ¨ group-assoc ğ“– a g h âŸ©
  (a Â· g) Â· h âˆ

-- Any Representable is the image of an included element

rep-inc : âˆ€ (f : Repr) â†’ Î£[ g âˆˆ âŸ¨ ğ“– âŸ© ] inc g â‰¡ fst f
rep-inc (a@(f , rest) , rf) = (f â‚) ,
  inverse-equality-lemma (inc (f â‚)) a (group-is-set ğ“–) (group-is-set ğ“–) Î» x â†’ sym (rf x â‚ x (sym (group-lid ğ“– x)))

-- We can now define incáµ£ and show it is an equivalence

incáµ£ : âŸ¨ ğ“– âŸ© â†’ Repr
incáµ£ g = inc g , inc-rep g

incáµ£-iso : Iso âŸ¨ ğ“– âŸ© Repr
incáµ£-iso .Iso.fun = incáµ£
incáµ£-iso .Iso.inv f = fst (rep-inc f)
incáµ£-iso .Iso.leftInv g = inc-injective (fst (rep-inc (incáµ£ g))) g (snd (rep-inc (incáµ£ g)))
incáµ£-iso .Iso.rightInv f = repr-equality (incáµ£ (fst (rep-inc f))) f (snd (rep-inc f))

incáµ£-equiv : âŸ¨ ğ“– âŸ© â‰ƒ Repr
incáµ£-equiv = isoToEquiv incáµ£-iso

-- And show it is a group isomorphism

incáµ£-homo : âˆ€ g h â†’ incáµ£ (g Â· h) â‰¡ incáµ£ g Â·âŸ¨ RSymGroup âŸ© (incáµ£ h)
incáµ£-homo g h = repr-equality (incáµ£ (g Â· h)) (incáµ£ g Â·âŸ¨ RSymGroup âŸ© incáµ£ h) (inc-homo g h)

incáµ£-group-iso : ğ“– â‰ƒ[ group-iso ] RSymGroup
incáµ£-group-iso = incáµ£-equiv , Î» where (g âˆ· h âˆ· []) â†’ incáµ£-homo g h

-- And so the two groups are equal

incâ‰¡ : ğ“– â‰¡ RSymGroup
incâ‰¡ = equivFun (GroupPath ğ“– RSymGroup) incáµ£-group-iso
